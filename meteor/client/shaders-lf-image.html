<body>
<script id="lf-image-fragment-shader" type="x-shader/x-fragment">
  precision mediump float;

  // our texture
  uniform sampler2D u_image;

  // the texCoords passed in from the vertex shader.
  varying vec2 v_texCoord;

  uniform vec2 u_gammaGain; // display parameters
  void main() {
    vec4 gamma = vec4(u_gammaGain.x, u_gammaGain.x, u_gammaGain.x, 1.0);
    vec4 gain = vec4(u_gammaGain.y, u_gammaGain.y, u_gammaGain.y, 1.0);
    gl_FragColor = pow(texture2D(u_image, v_texCoord) * gain, gamma);
  }
</script>

<script id="lf-pinhole-fragment-shader" type="x-shader/x-fragment">
  precision mediump float;

  // our texture
  uniform sampler2D u_image;

  // the texCoords passed in from the vertex shader (ST/focal plane)
  varying vec2 v_texCoord;
  // the observer's coordinates (UV/camera plane)
  uniform vec2 u_UVCoord;

  // lens grid
  uniform vec2 u_gridSize; // number of lens
  uniform vec2 u_rectOffset; // center of grid in texCoords
  uniform mat2 u_rectLinear; // grid intervals in texCoords
  uniform vec2 u_gammaGain; // display parameters
  void main(void) {
    vec4 gamma = vec4(u_gammaGain.x, u_gammaGain.x, u_gammaGain.x, 1.0);
    vec4 gain = vec4(u_gammaGain.y, u_gammaGain.y, u_gammaGain.y, 1.0);

    vec2 coord, coord0, coord1, coord2, coord3, coord4;
    vec4 fracts;
    vec4 color;

    // Coordinates in the lens grid space
    coord = v_texCoord.st * u_gridSize;
    // The interesting in-lens pixel
    coord0 = floor(coord) + u_UVCoord.xy;
    // Examine the four nearest lens, interpolating the final color
    coord1 = u_rectOffset + u_rectLinear * coord0;
    coord2 = u_rectOffset + u_rectLinear * (coord0 + vec2(0.0, 1.0));
    coord3 = u_rectOffset + u_rectLinear * (coord0 + vec2(1.0, 0.0));
    coord4 = u_rectOffset + u_rectLinear * (coord0 + vec2(1.0, 1.0));
    // Interpolation color based on the surrounding lens distance
    fracts = vec4(coord - floor(coord), floor(coord) + vec2(1.0, 1.0) - coord);
    color =   fracts.z * (  fracts.w * texture2D(u_image, coord1)
                          + fracts.y * texture2D(u_image, coord2))
            + fracts.x * (  fracts.w * texture2D(u_image, coord3)
                          + fracts.y * texture2D(u_image, coord4));

    gl_FragColor = pow(vec4(color.rgb, color.a) * gain, gamma);
  }
</script>
</body>
